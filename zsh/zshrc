#! /usr/bin/zsh
export ANTIBODYHOME="$HOME/.antibody"
export HISTFILE="$HOME/.bash_history"
export HISTSIZE=10000
export SAVEHIST=10000

# Vim Keys
bindkey -v
KEYTIMEOUT=1

function zle-line-init zle-keymap-select {
    case ${KEYMAP} in
        (vicmd) echo -ne '\e[1 q' ;;
        (*)     echo -ne '\e[5 q' ;;
    esac
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

# key bindings
bindkey '^[[A' up-line-or-history
bindkey '^[[B' down-line-or-history
bindkey '^[[1;5D' backward-word
bindkey '^[[1;5C' forward-word
bindkey "^[[H" beginning-of-line
bindkey "^[[1~" beginning-of-line
bindkey "^[[F" end-of-line
bindkey "^[[4~" end-of-line
bindkey '^[[3~' delete-char
bindkey '^A' kill-whole-line
bindkey '^X' sudo-command-line
bindkey '^[.' insert-last-word

# options
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
setopt AUTO_LIST
setopt NO_NOMATCH
setopt AUTO_CD

# helper functions
function exists() { command -v "$1" >/dev/null }

# config function
typeset -A config
function cfg() { eval nvim ${config[$1]} }

# antibody
source "$ANTIBODYHOME/load.zsh"
antibody bundle < "$ANTIBODYHOME/plugins"
config[antibody]="$ANTIBODYHOME/plugins"

# completions
export fpath=($HOME/.local/share/zsh/completions $fpath)
autoload -U compinit && compinit -d

# git
alias g=git
config[git]="~/.gitconfig"

# fzf
if exists fzf; then
    export FZF_DEFAULT_COMMAND='rg --files --follow --no-ignore-vcs --hidden --ignore-file /home/reeto/.local/share/fzf/gitignore 2>/dev/null'
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND='fd --type d --no-ignore-vcs --hidden --ignore-file /home/reeto/.local/share/fzf/gitignore 2>/dev/null'

    source /usr/share/fzf/completion.zsh
    source /usr/share/fzf/key-bindings.zsh

    if exists lpass; then
        alias cpass='lpass show -c --password $(lpass ls --format "%an (%au) %ai" | grep -v -e "^ " -e "()" | fzf --with-nth "..-2" | awk "{print \$NF}")'
    fi

    # bash ctrl+r with fzf
    fzf-history-widget-run() {
        fzf-history-widget
        zle accept-line
    }

    zle -N fzf-history-widget-run
    bindkey '^R' fzf-history-widget-run
fi

# editor
alias edit='$EDITOR'
if exists nvim; then
    function nvimbench() {
        bench="$(mktemp)" && /usr/bin/nvim --startuptime "$bench" "$@" && tail -1 "$bench" && rm -f "$bench"
    }
    config[nvim]="~/.config/nvim/config.vim ~/.config/nvim/plugins.vim"
fi

# package manager
if exists pacman; then
    function pkgdiff() {
        flags="-Qqe"
        hostnames=()

        for var in "$@"; do
            case "$var" in
                (-*) flags="$var" ;;
                (*) hostnames+="$var" ;;
            esac
        done

        if [[ ${#hostnames[@]} -eq 1 ]]; then
            icdiff -U 0 -L "$(hostname)" -L "${hostnames[1]}"  <(pacman $flags) <(ssh "${hostnames[1]}" "pacman $flags")
        elif [[ ${#hostnames[@]} -eq 2 ]]; then
            icdiff -U 0 -L "${hostnames[1]}" -L "${hostnames[2]}"  <(ssh "${hostnames[1]}" "pacman $flags") <(ssh "${hostnames[2]}" "pacman $flags")
        else
            echo "usage: pkgdiff [flags] <host 1> [host 2]"
            return 1
        fi
    }
    function binaries() { pacman -Qql $1 | grep /usr/bin | sed '1d; s/\/usr\/bin\///' }
fi

# chrome
exists google-chrome-stable && alias chrome="google-chrome-stable"

# file listing
exists exa && alias ls="exa -xF" && alias als="ls" || alias ls="ls -xF" && alias als="ls"
alias la="ls -a"
alias lh="ls -d .*"
alias ll="als -lF --git"

# filesystem operations
alias mv="mv -v"
alias rm="rm -iv"
alias cp="cp -v"
alias df="df -hl"
alias du="du -h"

# python
if exists bpython; then
    function python() { [ "$#" -eq 0 ] && bpython || /usr/bin/python "$@" }
fi

# SSH public key
function pub() { [ "$#" -eq 0 ] && cat "$HOME/.ssh/id_ecdsa.pub" || cat "$HOME/.ssh/id_$1.pub" }

# clipboard
if exists xclip; then
    function c() { tr -d '\n' | xclip -sel clip }
    function copy() { cat $1 | c }
    alias v="xclip -o -sel clip"
    alias cpub="pub | c | echo 'RSA public key copied.'"
fi

# IP
alias pubip="drill myip.opendns.com @resolver1.opendns.com | awk '!/;;/ && /IN/' | head -n 1 | awk '{ print \$NF  }'"
alias privip="hostname -i"

unset exists

# Config files
config[alacritty]="~/.config/alacritty/alacritty.yml"
config[dunst]="~/.config/dunst/dunstrc"
config[i3]="~/.config/i3/config"
config[latex]="~/.latexmkrc"
config[polybar]="~/.config/polybar/config"
config[ssh]="~/.ssh/config"
config[tmux]="~/.tmux.conf"
config[zathura]="~/.config/zathura/zathurarc"
config[zsh]="~/.zshrc ~/.zshenv"
